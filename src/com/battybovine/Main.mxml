<?xml version="1.0" encoding="utf-8"?>
<!--[CDATA[
 * Copyright (c) 2012 Jamie Greunbaum
 * 
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 * 
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 * 
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
]]>-->
<s:WindowedApplication
			xmlns:fx="http://ns.adobe.com/mxml/2009"
			xmlns:s="library://ns.adobe.com/flex/spark"
			xmlns:mx="library://ns.adobe.com/flex/mx"
			showStatusBar="false"
			creationComplete="init()"
			close="closeEvent()">
	
	<fx:Script>
		<![CDATA[
			import com.battybovine.PreferencesWindow;
			import com.battybovine.RenderWindow;
			
			import mx.controls.Alert;
			import mx.core.FlexGlobals;
			import mx.collections.ArrayCollection;
			import mx.events.CloseEvent;
			import mx.events.CollectionEvent;
			import mx.events.CollectionEventKind;
			import mx.events.MenuEvent;
			import mx.managers.PopUpManager;
			
			
			
			private var prefsman:PreferencesManager;
			private var prefswin:PreferencesWindow = new PreferencesWindow();
			private var renderwin:RenderWindow;
			
			private var swfcontroller:MovieClip = new MovieClip();
			private var filequeuedata:ArrayCollection = new ArrayCollection();
			
			
			
			private function init():void {
				// Check for changes to the data in the file queue list
				filequeuedata.addEventListener(CollectionEvent.COLLECTION_CHANGE, listChangeEventHandler);
				
				// Set the data provider for the file queue, and add change event listener
				filequeue.dataProvider = filequeuedata;
				filequeue.addEventListener(Event.CHANGE, listCommonEventHandler);
				
				// Add listeners for the application itself, to allow dropping files into the window and interacting with the file list
				FlexGlobals.topLevelApplication.addEventListener(NativeDragEvent.NATIVE_DRAG_ENTER, acceptDrop);
				FlexGlobals.topLevelApplication.addEventListener(NativeDragEvent.NATIVE_DRAG_DROP, handleDrop);
				FlexGlobals.topLevelApplication.addEventListener(KeyboardEvent.KEY_UP, globalKeyPressed);
				
				// Load the preferences, and fail if they can't be loaded
				if (!prefswin.createPrefsMan()) {
					Alert.show("Preferences failed to load. Exiting...", "", Alert.OK, this);
					this.closeEvent();
				}
				prefsman = prefswin.getPrefsMan();
				// Listen for changes to the resolution and aspect ratio so that we can properly update the preview image
				prefsman.addEventListener(PreferencesManager.OUTPUTRESOLUTION_CHANGED, prefsOutputResolutionChangedHandler);
				prefsman.addEventListener(PreferencesManager.ASPECT_CHANGED, prefsAspectChangedHandler);
				// Load preferences now that we're listening for changes
				prefswin.loadPrefsData();
			}
			// Listen for changes to the file list
			private function listChangeEventHandler(e:CollectionEvent):void {
				switch(e.kind) {
					case CollectionEventKind.REMOVE:
						// Disable the Render Selected button, since the selection is now deleted
						renderselected.enabled = false;
						// Unload any SWF that may be loaded into the preview window
						beginUnloadingSWF();
						break;
				}
				
				// Enable and disable the Render All button depending on the new contents of the list
				renderall.enabled = (filequeuedata.length > 0);
			}
			// Listen for user interaction with the file list
			private function listCommonEventHandler(e:Event):void {
				// Only allow a SWF file to remain loaded if only one is selected
				if (filequeue.selectedItems.length == 1) {
					beginLoadingSWF(filequeue.selectedItem as String);
					renderselected.enabled = true;
				} else {
					beginUnloadingSWF();
					renderselected.enabled = false;
				}
			}
			// Listen for changes to the resolution setting
			private function prefsOutputResolutionChangedHandler(e:Event):void {
				var ratio:Number = prefsman.getOutputWidth() / prefsman.getOutputHeight();
				if ((ratio * previewImage.maxHeight) > previewImage.maxWidth) {
					previewImage.width = previewImage.maxWidth;
					previewImage.height = previewImage.maxWidth * (1 / ratio);
				} else {
					previewImage.width = previewImage.maxHeight * ratio;
					previewImage.height = previewImage.maxHeight;
				}
			}
			// Listen for changes to the aspect ratio setting
			private function prefsAspectChangedHandler(e:Event):void {
				if(loadedSWF.source)
					updatePreviewImage();
			}
			
			// Accept file drops onto the main application window
			private function acceptDrop(e:Event):void {
				NativeDragManager.acceptDragDrop(this);
			}
			// Handle SWF file drops
			private function handleDrop(e:NativeDragEvent):void {
				var droppedfiles:Array = e.clipboard.getData(ClipboardFormats.FILE_LIST_FORMAT) as Array;
				for each (var file:File in droppedfiles) {
					switch(file.extension) {
						case "swf":
							handleOpenedSWFFile(file);
						default:
							break;
					}
				}
			}
			
			// Show a dialogue for opening SWF files
			private function showOpenSwfDialogue():void {
				var swfFilter:FileFilter = new FileFilter("SWF File (*.swf)", "*.swf");
				var swffile:File = new File();
				swffile.browseForOpen("Locate SWF file", [swfFilter]);
				swffile.addEventListener(Event.SELECT, swfSelectedFromDialogue);
			}
			private function swfSelectedFromDialogue(e:Event):void {
				handleOpenedSWFFile(e.currentTarget as File);
			}
			
			//private function playSWF():void						{ swfcontroller.play(); }
			//private function pauseSWF():void					{ swfcontroller.stop(); }
			private function seekbarTipFormat(i:Number):String	{ return i.toString(10); }
			
			// Add the file to the list when it's opened, whether via the dialogue or from being dropped into the window
			private function handleOpenedSWFFile(file:File):void {
				if(!filequeuedata.contains(file.nativePath)) {
					filequeuedata.addItem(file.nativePath);
				}
			}
			
			// Start loading the SWF file
			private function beginLoadingSWF(filepath:String):void {
				// Create a new loader and listen for loading to be complete
				var loader:Loader = new Loader();
				loader.contentLoaderInfo.addEventListener(Event.COMPLETE, loadSWF);
				// Load via a ForcibleLoader to add basic compatibility with AVM1 SWF files
				var fLoader:ForcibleLoader = new ForcibleLoader(loader);
				fLoader.load(new URLRequest(filepath));
				// Enable scrubbing through the SWF file
				seekbar.enabled = true;
			}
			// Start unloading the SWF file
			private function beginUnloadingSWF():void {
				// Unload the SWF and the preview image
				loadedSWF.unloadAndStop(true);
				previewImage.unloadAndStop(true);
				// Disable and reset the seekbar
				seekbar.enabled = false;
				seekbar.removeEventListener(Event.CHANGE, seekbarChangeHandler);
				seekbar.maximum = 1;
			}
			
			private function loadSWF(e:Event):void {
				// Set the container to the same dimensions as the loaded SWF, and set its source to the content
				loadedSWF.width = e.currentTarget.width;
				loadedSWF.height = e.currentTarget.height;
				loadedSWF.source = e.currentTarget.content;
				
				//trace("Loaded SWF container size: " + loadedSWF.width + ", " + loadedSWF.height);
				//trace("Loaded SWF source size:    " + loadedSWF.source.width + ", " + loadedSWF.source.height);
				//trace("Loaded SWF source scale:   " + loadedSWF.source.scaleX + ", " + loadedSWF.source.scaleY);
				
				// Configure a controller to listen for FRAME_CONSTRUCTED events to allow us to generate preview images at will
				swfcontroller = e.currentTarget.content as MovieClip;
				swfcontroller.addEventListener(Event.FRAME_CONSTRUCTED, swfLoadCompleteHandler);
				swfcontroller.gotoAndStop(1);
				
				// Initialise the seekbar and listen for user interaction
				seekbar.maximum = swfcontroller.totalFrames;
				seekbar.value = 1;
				seekbar.addEventListener(Event.CHANGE, seekbarChangeHandler);
			}
			// Listen for when the SWF file is fully loaded
			private function swfLoadCompleteHandler(e:Event):void {
				// Finish listening to FRAME_CONSTRUCTED events, to avoid generating previews repeatedly
				swfcontroller.removeEventListener(Event.FRAME_CONSTRUCTED, swfLoadCompleteHandler );
				swfcontroller.soundTransform = new SoundTransform(0, 0);
				renderselected.enabled = true;
				updatePreviewImage();
			}
			private function seekbarChangeHandler(e:Event):void {
				// Start listening for FRAME_CONSTRUCTED events again when the user wants to scrub through the SWF
				swfcontroller.addEventListener(Event.FRAME_CONSTRUCTED, swfLoadCompleteHandler);
				// Then go to the appropriate frame
				swfcontroller.gotoAndStop(seekbar.value);
			}
			private function updatePreviewImage():void {
				previewImage.source = RenderWindow.captureSWFFrame(loadedSWF, previewImage.width, previewImage.height, prefsman.getAspect());
			}
			
			
			// When the Render All button is clicked, prepare all items for output
			private function renderAllClicked():void {
				prepareItemsForOutput(true);
			}
			// When the Render Selected button is clicked, prepare the selected items for output
			private function renderSelectedClicked():void {
				prepareItemsForOutput(false);
			}
			private function prepareItemsForOutput(all:Boolean = true):Boolean {
				// Make sure the preferences are loaded before continuing
				//if (!prefsman.preferencesLoaded()) {
					//Alert.show("Preferences could not be loaded. Not gonna be able to render anything. Sorry.", "", Alert.OK, this);
					//return false;
				//}
				
				// Initialise the Render dialogue and display it
				showRenderWindow();
				
				// Find which array to load into the render dialogue depending on the button that was clicked
				if (all) {
					renderwin.loadFiles(filequeuedata.toArray());
				} else {
					renderwin.loadFiles(filequeue.selectedItems);
				}
				
				return true;
			}
			
			
			
			// Display the Preferences window
			private function showPreferencesWindow():void {
				PopUpManager.addPopUp(prefswin, this, true);
				PopUpManager.centerPopUp(prefswin);
			}
			
			// Show a placeholder About... dialogue for now
			private function showAboutDialogue():void {
				Alert.show("Show About dialogue here...", "About BatchFlash", Alert.OK, this, null, null, Alert.OK);
			}
			
			// Dispatch an EXITING event on quit to allow us to confirm exiting later if necessary
			private function quitApplication():void {
				var exitingEvent:Event = new Event(Event.EXITING, false, true);
				NativeApplication.nativeApplication.dispatchEvent(exitingEvent);
				if (!exitingEvent.isDefaultPrevented()) {
					NativeApplication.nativeApplication.exit();
				}
			}
			
			// Initialise and display the Render dialogue
			private function showRenderWindow():void {
				renderwin = PopUpManager.createPopUp(this, RenderWindow, true) as RenderWindow;
				PopUpManager.centerPopUp(renderwin);
			}
			
			
			/*
			// Handle key equivalents for the native menu bar
			private function keyEquivalentModifiers(item:Object):Array {
				var result:Array = new Array();
				
				var keyEquivField:String = mainmenu.keyEquivalentField;
				var altKeyField:String;
				var controlKeyField:String;
				var shiftKeyField:String;
				if (item is XML) {
					altKeyField = "@altKey";
					controlKeyField = "@controlKey";
					shiftKeyField = "@shiftKey";
				} else if (item is Object) {
					altKeyField = "altKey";
					controlKeyField = "controlKey";
					shiftKeyField = "shiftKey";
				}
				
				if (item[keyEquivField] == null || item[keyEquivField].length == 0) { return result; }
				
				if (item[altKeyField] != null && item[altKeyField] == true) {
					if ((Capabilities.os.indexOf("Windows") >= 0)) {
						result.push(Keyboard.ALTERNATE);
					}
				}
				if (item[controlKeyField] != null && item[controlKeyField] == true) {
					if (Capabilities.os.indexOf("Windows") >= 0) {
						result.push(Keyboard.CONTROL);
					} else if (Capabilities.os.indexOf("Mac OS") >= 0) {
						result.push(Keyboard.COMMAND);
					}
				}
				if (item[shiftKeyField] != null && item[shiftKeyField] == true) {
					result.push(Keyboard.SHIFT);
				}
				
				return result;
			}
			// Handle menu events for the native menu
			protected function menuItemEventHandler(e:FlexNativeMenuEvent):void {
				trace("Menu item:", e.label);
				if(e.label.match(/^open\sswf/i)) { showOpenSwfDialogue(); }			// Open SWF File...
				if(e.label.match(/^preferences/i)) { showPreferencesDialogue(); }	// Preferences
				if(e.label.match(/^quit/i)) { quitApplication(); }					// Quit
				if(e.label.match(/^about(\s.*)?\.\.\./i)) { showAboutDialogue(); }	// About...
			}
			*/
			
			// Listen for key presses globally to allow selecting all items and deleting selected items using the keyboard
			private function globalKeyPressed(e:KeyboardEvent):void {
				if (e.keyCode == Keyboard.A && e.controlKey == true) {
					filequeue.selectedItems = (filequeue.dataProvider as ArrayCollection).source;
				}
				if (e.keyCode == Keyboard.DELETE || e.keyCode == Keyboard.BACKSPACE) {
					if (filequeue.selectedItems != null && filequeue.selectedItems.length >= 0) {
						Alert.show("Are you sure you want to remove the selected files from the queue? This will not delete the actual files.", "",
						Alert.YES | Alert.NO, this, deleteFileQueueHandler, null, Alert.NO);
					}
				}
			}
			// Listen for confirmation that the user wants to delete the selected files
			private function deleteFileQueueHandler(e:CloseEvent):void {
				if (e.detail == Alert.YES) {
					for each(var item:Object in filequeue.selectedItems) {
						filequeuedata.removeItemAt(filequeuedata.getItemIndex(item.toString()));
					}
				}
			}
			
			// Listen for click events in the Flex menu bar
			protected function menuItemClickHandler(e:MenuEvent):void {
				trace("Menu item:", e.label);
				if(e.label.match(/^open\sswf/i)) { showOpenSwfDialogue(); }			// Open SWF File...
				if(e.label.match(/^preferences/i)) { showPreferencesWindow(); }	// Preferences
				if(e.label.match(/^quit/i)) { quitApplication(); }					// Quit
				if(e.label.match(/^about(\s.*)?\.\.\./i)) { showAboutDialogue(); }	// About...
			}
			// Listen for keyboard shortcuts 
			protected function menuItemKeyHandler(e:KeyboardEvent):void {
				return;
				//trace("Menu item:", e.label);
				//if(e.label.match(/^open\sswf/i)) { showOpenSwfDialogue(); }			// Open SWF File...
				//if(e.label.match(/^preferences/i)) { showPreferencesDialogue(); }	// Preferences
				//if(e.label.match(/^quit/i)) { quitApplication(); }					// Quit
				//if(e.label.match(/^about(\s.*)?\.\.\./i)) { showAboutDialogue(); }	// About...
			}
			
			
			
			// For now, just indicate we're shutting down
			private function closeEvent():void {
				trace("Shutting down...");
			}
		]]>
	</fx:Script>
	
	<!--<s:menu>
		<mx:FlexNativeMenu id="mainmenu" labelField="@label" keyEquivalentField="@keyEquivalent" keyEquivalentModifiersFunction="keyEquivalentModifiers" dataProvider="{mainmenudata}" showRoot="false" itemClick="menuItemEventHandler(event)" />
	</s:menu>
	<fx:Declarations>
	</fx:Declarations>-->
	
	<mx:MenuBar id="mainmenu" top="0" right="0" left="0" labelField="@label" showRoot="false" itemClick="menuItemClickHandler(event)" keyDown="menuItemKeyHandler(event)">
		<fx:XMLList id="mainmenudata">
			<mainmenu>
				<menuitem label="File">
					<menuitem id="mainmenuopen" label="Open SWF File..." keyEquivalent="o" controlKey="true" />
					<menuitem id="mainmenupreferences" label="Preferences" keyEquivalent="p" controlKey="true" />
					<menuitem type="separator" />
					<menuitem id="mainmenuquit" label="Quit" keyEquivalent="q" controlKey="true" />
				</menuitem>
				<menuitem label="Help">
					<menuitem id="mainmenuabout" label="About BatchFlash..." keyEquivalent="?" controlKey="true" />
				</menuitem>
			</mainmenu>
		</fx:XMLList>
	</mx:MenuBar>
	
	<mx:VBox id="container" width="100%" height="100%" horizontalAlign="center" paddingTop="{mainmenu.height+10}" paddingBottom="10" paddingLeft="10" paddingRight="10">
		<mx:VBox width="640" height="360" maxWidth="640" maxHeight="360" horizontalAlign="center" verticalAlign="middle">
			<mx:Image id="previewImage" width="640" height="360" maxWidth="640" maxHeight="360" opaqueBackground="true" horizontalAlign="center" verticalAlign="middle" />
		</mx:VBox>
		<mx:HSlider id="seekbar" width="100%" minimum="1" maximum="1" value="1" snapInterval="1" dataTipPrecision="0" dataTipFormatFunction="seekbarTipFormat" liveDragging="true" enabled="false" />
		<mx:List id="filequeue" width="100%" height="100%" allowMultipleSelection="true" verticalScrollPolicy="on" showDataTips="true" />
		<mx:HBox width="100%" horizontalAlign="right">
			<mx:Button id="renderpreferences" icon="@Embed(source='../../../res/gearicon.png')" width="28" click="showPreferencesWindow()" enabled="true" />
			<mx:Spacer width="100%" />
			<mx:Button id="renderselected" label="Render Selected" click="renderSelectedClicked()" enabled="false" />
			<mx:Button id="renderall" label="Render All" click="renderAllClicked()" enabled="false" />
		</mx:HBox>
	</mx:VBox>
	<mx:SWFLoader id="loadedSWF" scaleContent="false" showBusyCursor="true" visible="false" includeInLayout="false" />
	
</s:WindowedApplication>